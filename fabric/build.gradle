plugins {
    id 'fabric-loom' version '0.7-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.MOD_NAME
version = project.MC_VERSION_GENERAL + "-" + project.VERSION + "-" + project.MOD_LOADER
group = project.MAVEN_GROUP

repositories {
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.terraformersmc.com/" }
    maven { url = "https://maven.gegy.dev" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.MC_VERSION}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${project.LOADER_VERSION}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.FABRIC_VERSION}"

    includeFabricApiModule "fabric-key-binding-api-v1"
    includeFabricApiModule "fabric-lifecycle-events-v1"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.CLOTH_CONFIG_VERSION}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include("me.shedaniel.cloth:cloth-config-fabric:${project.CLOTH_CONFIG_VERSION}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation "com.terraformersmc:modmenu:${project.MODMENU_VERSION}"

    modRuntime ("supercoder79:databreaker:${project.DATABREAKER_VERSION}") {
        exclude module: "fabric-loader"
    }
}

def includeFabricApiModule(String moduleName) {
    Object dependency = fabricApi.module(moduleName, project.FABRIC_VERSION)
    dependencies.modImplementation(dependency)
    dependencies.include(dependency)
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {

    it.options.encoding = "UTF-8"
    def targetVersion = 8

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

curseforge {
    if (project.hasProperty('CF_API_KEY')){
        apiKey = project.getProperty('CF_API_KEY')
    }
    project {
        id = "${PROJECT_ID}"
        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')
        releaseType = "${RELEASE_TYPE}"
        addGameVersion "${MOD_LOADER}"
        addGameVersion "${JAVA_VERSION}"
        addGameVersion '1.16.5'
        addGameVersion '1.16.4'
        addGameVersion '1.16.3'
        addGameVersion '1.16.2'
        addGameVersion '1.16.1'
        mainArtifact(remapJar) {
            displayName = "${MOD_NAME}-${MC_VERSION_GENERAL}-${VERSION}-${MOD_LOADER}.jar"
            relations {
                requiredDependency 'cloth-config'
            }
        }
    }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}